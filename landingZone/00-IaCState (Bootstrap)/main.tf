################################################################
# Titre: IaC State Security (Bootstrap)
# Description : Backend Terraform sécurisé (Chiffrement KMS, Versioning)
# Auteur: Ravindra JOB | v2.0
# Update: 23/05/2026
################################################################

resource "aws_kms_key" "terraform_state_key" {
  description             = "Clé pour chiffrer le state Terraform"
  deletion_window_in_days = 30
  enable_key_rotation     = true
}

resource "aws_s3_bucket" "terraform_state" {
  bucket = "tfstate-lab-${var.random_suffix}"
  
  # Protection contre la destruction accidentelle
  lifecycle {
    prevent_destroy = true
  }
}

resource "aws_s3_bucket_versioning" "state_versioning" {
  bucket = aws_s3_bucket.terraform_state.id
  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "state_crypto" {
  bucket = aws_s3_bucket.terraform_state.id

  rule {
    apply_server_side_encryption_by_default {
      kms_master_key_id = aws_kms_key.terraform_state_key.arn
      sse_algorithm     = "aws:kms"
    }
  }
}

# Table DynamoDB pour le State Locking (évite les corruptions parallèles)
resource "aws_dynamodb_table" "terraform_locks" {
  name         = "tfstate-locks-lab"
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "LockID"

  attribute {
    name = "LockID"
    type = "S"
  }
}
